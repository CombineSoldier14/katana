# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=katanalibs
pkgver=4.18.0.b38566b
pkgrel=1
pkgdesc="Katana core libraries"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
depends=('libxss' 'krb5' 'qca' 'libdbusmenu-qt' 'perl'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr' 'strigi'
        'docbook-xsl' 'upower' 'udisks2' 'libxcursor' 'phonon-qt4' 'libraw'
        'media-player-info' 'libxtst' 'libutempter' 'qtwebkit' 'icu' 'exiv2'
        'libmusicbrainz5' 'alsa-lib' 'xcb-util-image' 'xcb-util-keysyms'
        'xcb-util-renderutil' 'libwebp')
makedepends=('cmake' 'git' 'avahi' 'libgl' 'hspell' 'mesa' 'grantlee-qt4')
install=${pkgname}.install
source=("git+https://github.com/fluxer/kdelibs.git"
        'kde-applications-menu.patch' 'archlinux-menu.patch')
sha1sums=('SKIP'
          '2ab1ac29d3e6ef1020a1781a79882ba586e6bdab'
          '4de0a8db4155a83667bc4dac1c3df8659ee4a030')
conflicts=('kdelibs' 'libkdcraw' 'libkexiv2' 'libkcddb' 'libkcompactdisc')

pkgver() {
    cd kdelibs
    printf "4.18.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
    cd kdelibs
    # avoid file conflict with gnome-menus and kservice
    patch -p1 -i "${srcdir}/kde-applications-menu.patch"
    # add Archlinux menu entry
    patch -p1 -i "${srcdir}/archlinux-menu.patch"
}

build() {
    cd build
    cmake ../kdelibs \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        -DLOCALE_INSTALL_DIR=/usr/share/locale/kde4 \
        -DKDE_DEFAULT_HOME='.katana' \
        -DWITH_FAM=OFF
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    # cert bundle seems to be hardcoded, link it to the one from ca-certificates
    ln -vsf /etc/ssl/certs/ca-certificates.crt \
        "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
