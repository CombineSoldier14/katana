# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katanalibs
pkgver=4.24.0.r4502.0f0dcc7b
pkgrel=1
pkgdesc="Katana core libraries"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://osdn.net/projects/kde/'
license=('GPL' 'LGPL')
groups=('katana')
depends=('katie-git' 'enchant' 'libwebp' 'freetype2' 'libdeflate' 'djvulibre'
         'exiv2' 'mpv' 'avahi' 'curl' 'taglib' 'ebook-tools' 'poppler'
         'shared-mime-info' 'media-player-info' 'libxtst' 'libarchive'
         'libtiff' 'libraw' 'libjpeg-turbo' 'openjpeg2' 'libspectre')
makedepends=('cmake' 'git')
source=(
    "git+https://scm.osdn.net/gitroot/kde/kdelibs.git"
    # "git+https://bitbucket.org/smil3y/kdelibs.git"
    # "git+https://github.com/fluxer/kdelibs.git"
)
sha1sums=('SKIP')

pkgver() {
    cd kdelibs
    printf "4.24.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
    cd kdelibs
}

build() {
    cd build
    cmake ../kdelibs \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_SYSCONF_INSTALL_DIR=/etc \
        -DKDE4_LIBEXEC_INSTALL_DIR=/usr/lib/kde4 \
        -DKDE4_SBIN_INSTALL_DIR=/usr/bin
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
