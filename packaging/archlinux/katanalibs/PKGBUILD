# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=katanalibs
pkgver=4.18.0.b7febc71
pkgrel=1
pkgdesc="Katana core libraries"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
depends=('libxss' 'krb5' 'qt4' 'perl' 'shared-mime-info' 'enchant'
        'jasper' 'openexr' 'strigi' 'upower' 'udisks2' 'libxcursor' 'mpv'
         'libraw' 'media-player-info' 'libxtst' 'libutempter' 'icu'
         'exiv2' 'libmusicbrainz5' 'alsa-lib' 'xcb-util-image'
         'xcb-util-keysyms' 'xcb-util-renderutil' 'libwebp')
makedepends=('cmake' 'git' 'avahi' 'libgl' 'mesa')
source=("git+https://github.com/fluxer/kdelibs.git"
        'kde-applications-menu.patch' 'archlinux-menu.patch')
sha1sums=('SKIP'
          '2ab1ac29d3e6ef1020a1781a79882ba586e6bdab'
          '4de0a8db4155a83667bc4dac1c3df8659ee4a030')
conflicts=('kdelibs' 'libkdcraw' 'libkexiv2' 'libkcddb' 'libkcompactdisc')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd kdelibs
    printf "4.18.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
    cd kdelibs
    # avoid file conflict with gnome-menus and kservice
    patch -p1 -i "${srcdir}/kde-applications-menu.patch"
    # add Archlinux menu entry
    patch -p1 -i "${srcdir}/archlinux-menu.patch"
}

build() {
    cd build
    cmake ../kdelibs \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        -DLOCALE_INSTALL_DIR=/usr/share/locale/kde4 \
        -DWITH_FAM=OFF \
        -DWITH_KATIE=OFF \
        -DWITH_DBusMenuQt=OFF
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
