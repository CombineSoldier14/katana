# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=strigi
pkgver=0.7.8.8024c8c
pkgrel=1
pkgdesc="File indexing and file search framework adopted by KDE"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('LGPL')
groups=('katana')
depends=('bzip2' 'dbus' 'exiv2' 'ffmpeg' 'libxml2' 'zlib')
makedepends=('cmake' 'git')
source=("git+https://github.com/fluxer/strigi.git")
sha1sums=('SKIP')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd strigi
    printf "0.7.8.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../strigi \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_QT4=FALSE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
