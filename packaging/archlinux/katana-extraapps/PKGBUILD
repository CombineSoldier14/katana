# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# NOTE: eigen2 you can build from AUR
# TODO: split into small packages
# TODO: poppler-qt4

pkgname=katana-extraapps
pkgver=4.19.0.r554.8bd06534
pkgrel=1
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
pkgdesc='Katana extra applications'
url="http://fluxer.github.io/katana/"
depends=("katanalibs>=4.19.0" "katana-workspace>=4.19.0"
        'taglib' 'sane' 'qjson' 'libvncserver' 'gettext' 'libgpod'
        'libcanberra' 'openconnect' 'libmms' 'libofa' 'speech-dispatcher'
        'freerdp' 'libspectre' 'eigen2' 'djvulibre' 'qca-qt4' 'qimageblitz'
        'libarchive' 'ffmpeg' 'parted')
makedepends=('cmake' 'git' 'boost')
optdepends=('samba: sharing files and directories'
        'cronie: task scheduling')
source=("git+https://github.com/fluxer/kde-extraapps.git")
sha1sums=('SKIP')
conflicts=('kdeutils-ark' 'kdevelop' 'kdevplatform')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd kde-extraapps
    printf "4.19.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-extraapps \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_KATIE=OFF
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
