# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# TODO: split into small packages

pkgname=katana-extraapps
pkgver=4.18.0.2e8a411
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
pkgdesc='Katana extra applications'
url="http://fluxer.github.io/katana/"
install="$pkgname.install"
depends=("katanalibs>=4.16.0" "katana-workspace>=4.16.0"
        'taglib' 'libmariadbclient' 'sane' 'libarchive' 'poppler-qt4' 'qjson'
        'exiv2' 'lcms2' 'libvncserver' 'ffmpeg' 'gettext' 'libmtp' 'libgl'
        'parted' 'libcanberra' 'libgpod' 'openconnect' 'libmms' 'libofa'
        'speech-dispatcher' 'liblastfm' 'grantlee-qt4' 'libmygpo-qt' 'freerdp'
        'networkmanager-qt4' 'mobile-broadband-provider-info' 'libspectre'
        'eigen2' 'djvulibre')
makedepends=('cmake' 'git')
optdepends=('samba: sharing files and directories'
        'cronie: task scheduling')
source=("git+https://github.com/fluxer/kde-extraapps.git")
sha1sums=('SKIP')
conflicts=('kdeutils-ark' 'kdevelop' 'kdevplatform')

pkgver() {
    cd kde-extraapps
    printf "4.18.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-extraapps \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}


