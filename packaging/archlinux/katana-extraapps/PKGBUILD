# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katana-extraapps
pkgver=4.22.0.r1181.4815a57d
pkgrel=1
pkgdesc="Katana extra applications"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://osdn.net/projects/kde/'
license=('GPL' 'LGPL')
groups=('katana')
depends=("katie-git" "katanalibs>=4.22.0" "katana-workspace>=4.22.0"
        'ffmpegthumbnailer' 'libtorrent-rasterbar' 'djvulibre' 'parted'
        'libmms' 'libatasmart' 'libspeechd' 'libvncserver')
makedepends=('cmake' 'git' 'xorg-server-devel' 'boost')
optdepends=(
    'libxslt: SSML support in Jovie'
    'qemu: Virtual machine management'
    'freerdp: A free Remote Desktop Protocol (RDP) Implementation'
    'ghostscript: PostScript, PDF and DvI support in KIO thumbnailer and Okular'
    'texlive-bin: DVI support in Okular'
)
source=("git+https://scm.osdn.net/gitroot/kde/kde-extraapps.git")
sha1sums=('SKIP')

pkgver() {
    cd kde-extraapps
    printf "4.22.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-extraapps \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
