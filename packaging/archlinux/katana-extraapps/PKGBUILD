# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katana-extraapps
pkgver=4.23.0.r1356.bf912109
pkgrel=1
pkgdesc="Katana extra applications"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://osdn.net/projects/kde/'
license=('GPL' 'LGPL')
groups=('katana')
depends=("katie-git" "katanalibs>=4.23.0" "katana-workspace>=4.23.0"
        'ffmpegthumbnailer' 'libtorrent-rasterbar' 'djvulibre' 'parted'
        'libmms' 'libatasmart' 'libvncserver' 'libarchive' 'libspectre')
makedepends=('cmake' 'git' 'xorg-server-devel' 'boost')
optdepends=(
    'qemu: Virtual machine management'
    'freerdp: A free Remote Desktop Protocol (RDP) Implementation'
    'texlive-bin: LaTeX support in Okular'
    'unrar: RAR write support in Ark'
)
source=("git+https://scm.osdn.net/gitroot/kde/kde-extraapps.git")
sha1sums=('SKIP')

pkgver() {
    cd kde-extraapps
    printf "4.23.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-extraapps \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
