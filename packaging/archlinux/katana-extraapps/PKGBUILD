# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# TODO: split into small packages

pkgname=katana-extraapps
pkgver=4.18.0.3e76cb7
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
pkgdesc='Katana extra applications'
url="http://fluxer.github.io/katana/"
depends=("katanalibs>=4.18.0" "katana-workspace>=4.18.0"
        'taglib' 'sane' 'libarchive' 'poppler-qt4' 'qjson' 'exiv2' 'lcms2'
        'libvncserver' 'ffmpeg' 'gettext' 'libmtp' 'libgl' 'parted' 'libgpod'
        'libcanberra' 'openconnect' 'libmms' 'libofa' 'speech-dispatcher'
        'liblastfm' 'libmygpo-qt' 'freerdp' 'libspectre' 'eigen2' 'djvulibre'
        'qimageblitz')
makedepends=('cmake' 'git')
optdepends=('samba: sharing files and directories'
        'cronie: task scheduling')
source=("git+https://github.com/fluxer/kde-extraapps.git")
sha1sums=('SKIP')
conflicts=('kdeutils-ark' 'kdevelop' 'kdevplatform')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd kde-extraapps
    printf "4.18.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-extraapps \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_KATIE=OFF
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}


