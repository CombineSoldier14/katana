# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=libkexiv2
pkgver=4.16.0.b849505
pkgrel=1
pkgdesc="A library to manipulate pictures metadata (for use with Katana)"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('GPL')
groups=('katana')
depends=('katanalibs>=4.16.0' 'exiv2')
source=("git+https://github.com/fluxer/kde-playground.git")
sha1sums=('SKIP')
options=('debug')

pkgver() {
      cd kde-playground
      printf "4.16.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
       mkdir -p build
}

build() {
       cd build
       export PATH="/usr/lib/qt4/bin:$PATH"
       cmake ../kde-playground/libkexiv2 \
               -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DCMAKE_INSTALL_PREFIX=/usr
       make
}

package() {
       cd "${srcdir}/build"
       make DESTDIR="${pkgdir}" install
}
