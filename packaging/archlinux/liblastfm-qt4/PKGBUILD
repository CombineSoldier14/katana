# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=liblastfm-qt4
pkgver=1.0.9.2ce2bfe
pkgrel=1
pkgdesc="A Qt C++ library for the Last.fm webservices"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://github.com/lastfm/liblastfm/'
license=('GPL3')
groups=('katana')
depends=('qt4' 'fftw' 'libsamplerate')
makedepends=('cmake' 'git')
source=("git+https://github.com/lastfm/liblastfm.git")
sha1sums=('SKIP')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd liblastfm
    printf "1.0.9.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../liblastfm \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DBUILD_WITH_QT4=ON
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
