# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=libkdcraw
pkgver=4.16.0.41bd5bc
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture (for use with Katana)"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('GPL')
groups=('katana')
depends=('katanalibs>=4.16.0' 'libraw')
source=("git+https://github.com/fluxer/kde-playground.git")
sha1sums=('SKIP')
conflicts=('libkdcraw')
provides=('libkdcraw')
options=('debug')

pkgver() {
      cd kde-playground
      printf "4.16.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
       mkdir -p build
}

build() {
       cd build
       export PATH="/usr/lib/qt4/bin:$PATH"
       cmake ../kde-playground/libkdcraw \
               -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DCMAKE_INSTALL_PREFIX=/usr
       make
}

package() {
       cd "${srcdir}/build"
       make DESTDIR="${pkgdir}" install
}
