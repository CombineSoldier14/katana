# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgbase=katana-l10n
pkgver=4.17.0.09de702
pkgrel=1
pkgdesc="Katana translations"
arch=('any')
url='http://fluxer.github.io/katana/'
license=('LGPL')
groups=('katana')
makedepends=('cmake' 'git')
source=("git+https://github.com/fluxer/kde-l10n.git")
sha1sums=('SKIP')

_languages=(
  'ar            "Arabic"'
  'ast           "Asturianu"'
  'be            "Belarusian"'
  'bg            "Bulgarian"'
  'bs            "Bosnian"'
  'ca            "Catalan"'
  'ca@valencia   "Catalan (Valencia)"'
  'cs            "Czech"'
  'da            "Danish"'
  'de            "German"'
  'el            "Greek"'
  'en_GB         "English (GB)"'
  'eo            "Esperanto"'
  'es            "Spanish"'
  'et            "Estonian"'
  'eu            "Basque"'
  'fa            "Persian"'
  'fi            "Finnish"'
  'fr            "French"'
  'ga            "Irish"'
  'gl            "Galician"'
  'he            "Hebrew"'
  'hne          "Chhattisgarhi"'
  'hi            "Hindi"'
  'hr            "Croatian"'
  'hu            "Hungarian"'
  'ia            "Interlingua"'
  'is            "Icelandic"'
  'it            "Italian"'
  'ja            "Japanese"'
  'kk            "Kazakh"'
  'km            "Khmer (Cambodia)"'
  'ko            "Korean"'
  'ku            "Kurdish"'
  'lt            "Lithuanian"'
  'lv            "Latvian"'
  'mai           "Maithili"'
  'mr            "Marathi"'
  'ms            "Malay"'
  'nb            "Norwegian Bokmal"'
  'nds           "Low Saxon"'
  'nl            "Dutch"'
  'nn            "Norwegian Nynorsk"'
  'oc            "Occitan"'
  'pa            "Punjabi"'
  'pl            "Polish"'
  'pt            "Portuguese"'
  'pt_BR         "Portuguese (Brasil)"'
  'ro            "Romanian"'
  'ru            "Russian"'
  'se            "Northern Sami"'
  'si            "Singhalese"'
  'sk            "Slovak"'
  'sl            "Slovenian"'
  'sr            "Serbian"'
  'sr@ijekavian  "Serbian (Ijekavian)"'
  'sr@ijekavianlatin "Serbian (Ijekavian Latin)"'
  'sr@latn       "Serbian (Latin)"'
  'sv            "Swedish"'
  'tg            "Tajik"'
  'th            "Thai"'
  'tr            "Turkish"'
  'ug            "Uighur"'
  'uk            "Ukrainian"'
  'wa            "Walloon"'
  'zh_CN         "Chinese (simplified)"'
  'zh_TW         "Chinese (traditional)"'
)

pkgver() {
    cd kde-l10n
    printf "4.17.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-l10n \
        -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
        -DENABLE_TESTING=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

_package() {
    pkgdesc="Katana $2 translations"
    depends=('katanalibs>=4.17.0') # not really

    make -C "build/$1" DESTDIR="${pkgdir}" install
}

for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    _pkgname=katana-l10n-$_locale

    pkgname+=($_pkgname)
    eval "package_${_pkgname}() {
        _package $_lang
    }"
done
