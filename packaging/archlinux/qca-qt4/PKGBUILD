# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=qca-qt4
pkgver=2.2.1
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('i686' 'x86_64')
url='https://userbase.kde.org/QCA'
license=('LGPL')
groups=('katana')
depends=('qt4' 'nss' 'ca-certificates')
makedepends=('cmake' 'doxygen' 'pkcs11-helper' 'botan')
optdepends=(
    'pkcs11-helper: PKCS-11 plugin'
    'botan: botan plugin'
)
source=("https://download.kde.org/stable/qca/$pkgver/qca-$pkgver.tar.xz")
sha1sums=('012f1259126f1ce40402f236c7531f3e1d1c89da')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../qca-$pkgver \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DQT4_BUILD=ON
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
