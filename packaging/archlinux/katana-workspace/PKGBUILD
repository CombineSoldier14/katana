# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katana-workspace
pkgver=4.23.0.r2178.2f78f4d8
pkgrel=1
pkgdesc="Katana workspace interface, base applications and tools"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://osdn.net/projects/kde/'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
depends=("katie-git" "katanalibs>=4.23.0"
        'libxdamage' 'libqalculate' 'libdrm' 'libgphoto2' 'lightdm'
        'libxklavier' 'libxres' 'libxcomposite' 'libxft' 'accountsservice'
        'xorg-xrdb' 'xorg-xsetroot' 'xorg-xrandr' 'xorg-xmessage' 'libusb'
        'xcb-util-renderutil' 'xcb-util-keysyms' 'ttf-dejavu' 'libssh'
        'pciutils' 'glu' 'mesa' 'libmtp' 'ebook-tools' 'xorg-server'
        'lm_sensors' 'djvulibre' 'ntp' 'qrencode' 'icoutils')
makedepends=('cmake' 'git' 'xorg-server-devel')
optdepends=(
    'gdb: Backtraces support'
    'mtools: KIO floppy support'
    'ctags: Code indexing support in Kate'
    'cppcheck: Code analysis support in Kate'
    'lrzsz: ZModem support in Konsole'
    'lsof: Open files widget (KLsofWidget)'
    'breezy: Bazaar support in Dolphin'
    'git: Git support in Dolphin'
    'mercurial: Mercurial support in Dolphin'
    'subversion: Subversion support in Dolphin'
)
backup=('etc/lightdm/lightdm-kgreeter-greeter.conf')
source=("git+https://scm.osdn.net/gitroot/kde/kde-workspace.git")
sha1sums=('SKIP')

pkgver() {
    cd kde-workspace
    printf "4.23.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-workspace \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
