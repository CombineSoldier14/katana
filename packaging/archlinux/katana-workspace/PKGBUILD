# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katana-workspace
pkgver=4.24.0.r2646.f6818c64
pkgrel=1
pkgdesc="Katana workspace interface, base applications and tools"
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://bitbucket.org/smil3y/workspace/projects/KATANA'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
depends=("katie-git" "katanalibs>=4.24.0"
        'libxdamage' 'libqalculate' 'libdrm' 'libgphoto2' 'lightdm' 'libgit2'
        'libxklavier' 'libxres' 'libxcomposite' 'libxft' 'accountsservice'
        'xorg-xrdb' 'xorg-xsetroot' 'xorg-xrandr' 'xorg-xmessage' 'libusb'
        'xcb-util-renderutil' 'xcb-util-keysyms' 'ttf-dejavu' 'libssh'
        'pciutils' 'glu' 'mesa' 'libmtp' 'ebook-tools' 'xorg-server'
        'lm_sensors' 'djvulibre' 'ntp' 'qrencode' 'icoutils')
makedepends=('cmake' 'git' 'xorg-server-devel')
optdepends=(
    'ctags: Code indexing support in Kate'
    'cppcheck: Code analysis support in Kate'
    'lrzsz: ZModem support in Konsole'
    'lsof: Open files widget (KLsofWidget)'
)
backup=('etc/lightdm/lightdm-kgreeter-greeter.conf')
source=(
    # "git+https://scm.osdn.net/gitroot/kde/kde-workspace.git"
    "git+https://bitbucket.org/smil3y/kde-workspace.git"
    # "git+https://github.com/fluxer/kde-workspace.git"
)
sha1sums=('SKIP')

pkgver() {
    cd kde-workspace
    printf "4.24.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-workspace \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
