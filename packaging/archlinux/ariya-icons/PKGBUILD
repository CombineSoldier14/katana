# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=ariya-icons
pkgver=4.18.0.6b7a09b
pkgrel=1
pkgdesc="Ariya icon theme for use with Katana"
arch=('any')
url='http://fluxer.github.io/katana/'
license=('LGPL')
groups=('katana')
depends=('gtk-update-icon-cache')
makedepends=('cmake' 'git')
install=${pkgname}.install
source=("git+https://github.com/fluxer/ariya-icons.git")
sha1sums=('SKIP')

pkgver() {
    cd ariya-icons
    printf "4.18.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../ariya-icons \
        -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
        -DENABLE_TESTING=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
