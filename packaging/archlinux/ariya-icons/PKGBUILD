# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=ariya-icons
pkgver=4.19.0.r21.54ae81b0
pkgrel=1
pkgdesc="Ariya icon theme for use with Katana"
arch=('any')
url='http://fluxer.github.io/katana/'
license=('LGPL')
groups=('katana')
makedepends=('cmake' 'git')
source=("git+https://github.com/fluxer/ariya-icons.git")
sha1sums=('SKIP')
options=('!strip' '!zipman')

pkgver() {
    cd ariya-icons
    printf "4.19.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../ariya-icons \
        -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
        -DENABLE_TESTING=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
