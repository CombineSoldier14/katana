# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=katana-baseapps
pkgver=4.18.0.r328.2198cc4
pkgrel=1
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
pkgdesc='Katana core applications'
optdepends=('katana-extraapps: thumbnailers for development, audio, video and other common files'
            'icoutils: thumbnailers for MS files'
            'ruby: servicemenu installation'
            'cppcheck: code analysis in Kate'
            'ctags: lookup definitions/declarations in Kate'
            'sudo: execute programs as user other than the current')
url="http://fluxer.github.io/katana/"
depends=("katanalibs>=4.18.0" 'qjson')
makedepends=('cmake' 'git') # TODO: pykde4
source=("git+https://github.com/fluxer/kde-baseapps.git")
sha1sums=('SKIP')
conflicts=('kdebase-dolphin'
           'kdebase-kdepasswd'
           'kdebase-kdialog'
           'kdebase-keditbookmarks'
           'kdebase-kfind'
           'kdebase-konq-plugins'
           'kdebase-konqueror'
           'kdebase-lib'
           'kdebase-plasma'
           'kdebase-katepart'
           'katepart4')

export CFLAGS="${CFLAGS} -flto" CXXFLAGS="${CXXFLAGS} -flto"

pkgver() {
    cd kde-baseapps
    printf "4.18.0."r%s.%s "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../kde-baseapps \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_KATIE=OFF
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir" install
}

