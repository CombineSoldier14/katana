# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=katana-baseapps
pkgver=4.17.0.c73cf79
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('katana')
pkgdesc='Katana core applications'
optdepends=('katana-extraapps: thumbnailers for development, audio, video and other common files'
            'icoutils: thumbnailers for MS files'
            'ruby: servicemenu installation')
url="http://fluxer.github.io/katana/"
install="$pkgname.install"
depends=("katanalibs>=4.16.0" 'qjson')
makedepends=('cmake' 'git' 'tidyhtml') # TODO: pykde4
source=("git+https://github.com/fluxer/kde-baseapps.git")
sha1sums=('SKIP')
options=('debug')

conflicts=('kdebase-dolphin'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-keditbookmarks'
         'kdebase-kfind'
         'kdebase-konq-plugins'
         'kdebase-konqueror'
         'kdebase-lib'
         'kdebase-plasma'
         'kdebase-katepart' 'katepart4')

pkgver() {
    cd kde-baseapps
    printf "4.17.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    export PATH="/usr/lib/qt4/bin:$PATH"
    cmake ../kde-baseapps \
        -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
        -DENABLE_TESTING=OFF \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir" install
}


