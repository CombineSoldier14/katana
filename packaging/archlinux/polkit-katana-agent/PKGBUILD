# $Id$
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

pkgname=polkit-katana-agent
pkgver=4.17.0.f8cb88d
pkgrel=1
pkgdesc="Daemon providing a polkit authentication UI for Katana"
arch=('i686' 'x86_64')
url='http://fluxer.github.io/katana/'
license=('GPL')
groups=('katana')
depends=('katanalibs>=4.16.0' 'polkit-qt4')
makedepends=('cmake' 'git')
source=("git+https://github.com/fluxer/kde-playground.git")
sha1sums=('SKIP')
conflicts=('polkit-kde-agent')
options=('debug')

pkgver() {
      cd kde-playground
      printf "4.17.0.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
       mkdir -p build
}

build() {
       cd build
       export PATH="/usr/lib/qt4/bin:$PATH"
       cmake ../kde-playground/polkit-kde-agent \
               -DCMAKE_BUILD_TYPE=RelWithDbgInfo \
               -DENABLE_TESTING=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DCMAKE_INSTALL_PREFIX=/usr
       make
}

package() {
       cd "${srcdir}/build"
       make DESTDIR="${pkgdir}" install
}
